apply plugin: 'com.android.application'

Properties props = new Properties()
try {
    props.load(file('fabric.properties').newDataInputStream())
} catch (final Exception ignored) {
    throw new GradleException("Missing fabric.properties, check the fabric.properties file.");
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    productFlavors {
        standard {
            applicationId "org.twizer.android"
            minSdkVersion 16
            targetSdkVersion 22
            versionCode 3
            versionName "1.1"
            multiDexEnabled = false
        }
    }

    signingConfigs {
        release {
            storeFile new File("twizer.keystore")
            storePassword(new File("twizer.pwd").text)
            keyAlias("twizer")
            keyPassword(new File("twizerstorage.pwd").text)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            zipAlignEnabled true
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
        }

        debug {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            minifyEnabled false
            zipAlignEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        absolutePaths true

        warningsAsErrors true

        enable 'IconExpectedSize', 'LogConditional', 'NegativeMargin', 'NewerVersionAvailable',
                'SelectableText', 'TypographyQuotes'
    }
}

repositories {
    maven { url 'https://dl.bintray.com/drummer-aidan/maven' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
    maven { url 'https://maven.fabric.io/public' }
}

dependencies {
    compile project(':ripple')
    compile project(':persistentSearch')

    compile 'com.android.support:design:22.2.0'
    compile 'com.google.android.gms:play-services-location:7.5.0'

    compile 'org.jetbrains:annotations:13.0'

    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.jakewharton:butterknife:6.1.0'

    compile('com.twitter.sdk.android:twitter:1.6.0@aar') {
        transitive = true;
    }

    compile 'org.apache.commons:commons-lang3:3.4'

    compile 'com.jpardogo.googleprogressbar:library:1.1.0'
    compile 'com.afollestad:material-dialogs:0.7.5.5'
    compile 'org.adw.library:discrete-seekbar:1.0.0'
}
