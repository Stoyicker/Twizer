buildscript {
    repositories {
        jcenter()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.18.0'
        classpath 'me.tatarka:gradle-retrolambda:3.0.1'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'io.fabric'
apply plugin: 'me.tatarka.retrolambda'

Properties props = new Properties()
try {
    props.load(file('crashlytics.properties').newDataInputStream())
} catch (Exception ignored) {
    throw new GradleException("Missing crashlytics.properties, check the crashlytics.properties file.");
}

android {
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    productFlavors {
        standard {
            applicationId "org.jorge.twizer"
            minSdkVersion 21
            targetSdkVersion 22
            versionCode 1
            versionName "0"
        }
    }

    signingConfigs {
        release {
            storeFile new File("twizer.keystore")
            storePassword(new File("twizer.pwd").text)
            keyAlias("twizer")
            keyPassword(new File("twizerstorage.pwd").text)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildTypes {
        release {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            buildConfigField "Boolean", "USE_CRASHLYTICS", "true"
            ext.enableCrashlytics = true
            minifyEnabled true
            proguardFiles 'proguard-rules.pro'
            zipAlignEnabled true
            signingConfig signingConfigs.release
        }

        debug {
            buildConfigField "String", "TWITTER_CONSUMER_KEY", "\"${props.getProperty("twitterConsumerKey")}\""
            buildConfigField "String", "TWITTER_CONSUMER_SECRET", "\"${props.getProperty("twitterConsumerSecret")}\""
            buildConfigField "Boolean", "USE_CRASHLYTICS", "Boolean.FALSE"
            ext.enableCrashlytics = true
            minifyEnabled false
            zipAlignEnabled false
        }

        travis.initWith(buildTypes.debug)

        travis {
            ext.enableCrashlytics = false
        }
    }

    lintOptions {
        absolutePaths true

        warningsAsErrors true

        enable 'IconExpectedSize', 'LogConditional', 'NegativeMargin', 'NewerVersionAvailable',
                'SelectableText', 'TypographyQuotes'
    }
}

repositories {
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    maven { url "https://jitpack.io" }
    maven { url "https://clojars.org/repo/" }
}

dependencies {
    compile 'com.google.code.gson:gson:2.3'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.3.0'
    compile 'com.squareup.okhttp:okhttp:2.3.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    debugCompile 'com.facebook.stetho:stetho:1.1.1'
    travisCompile 'com.facebook.stetho:stetho:1.1.1'
    //TODO Replace this with my fork
    compile 'com.quinny898.library.persistentsearch:library:1.0.0-SNAPSHOT'
    compile 'frankiesardo:icepick:3.0.3-SNAPSHOT'
    compile 'com.afollestad:material-dialogs:0.7.2.8'
    compile 'com.android.support:appcompat-v7:22.1.1'
    compile('com.crashlytics.sdk.android:crashlytics:2.2.3@aar') {
        transitive = true;
    }
    compile 'com.jakewharton:butterknife:6.1.0'
    compile project(':ripple')
    compile('com.twitter.sdk.android:twitter:1.4.0@aar') {
        transitive = true;
    }
}
